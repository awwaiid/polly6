#!/usr/bin/env perl6

use File::Find;

# This is the mapping we use to play a file
my @play_commands =
  /\.ogg$/             => 'ogg123',
  /\.mp3$/             => 'mpg123',
  /\.m4a$/             => 'mplayer',
  /^http:\/\//         => 'vlc',
  /\.(mod|xm|s3m|it)$/ => 'vlc -I curses',
  /.*/                 => 'vlc', # fallback
;

#| Recurse through the given "songs" expanding directories
sub expand_songs($verbose, @songs) {
  my @expanded_songs = [];

  for @songs -> $song {
    if $song.IO ~~ :d {
      my @dirsongs;
      say "Adding all files in $song" if $verbose;
      push @expanded_songs, |find(:dir<<$song>>, :type<file>).sort;
    } else {
      say "Adding $song" if $verbose;
      push @expanded_songs, $song;
    }
  }
  return @expanded_songs;
}

#| Format and display an on-screen announcement
sub osd_song($song) {
  my $saysong = "$song";
  $saysong ~~ s/^.*\///;  # Strip out directory
  $saysong ~~ s/\..*?$//; # remove extension
  $saysong ~~ s/ \s \- \s/,\n/;
  say "notify-send '$saysong'";
  shell("notify-send '$saysong'");
}

#| Make a song pronounceable and then send it to festival
sub announce_song($song) {
  my $saysong = "$song";
  $saysong ~~ s/.*\///; # Strip out directory
  $saysong ~~ s/\s\-\s\d\d\d?\s\-\s/ - /; # Strip out track number
  $saysong ~~ s/\....$//; # remove 3-letter extension
  $saysong ~~ s:g/_+/ /;

  # Turn separators into a pause (two newlines will work)
  $saysong ~~ s:g/\s \- \s /\n\n/;
  $saysong ~~ s:g/\&/ and /;
  $saysong ~~ s:g/\'//;

  say "Announcing: $song";
  shell("say '$saysong'");
}

my $last_sigint = now;

signal(Signal::SIGINT).tap: {
  exit if now - $last_sigint < 0.5;
  $last_sigint = now;
  say "\nSkipping to next song...";
}

#| Play an individual song file, maybe hiding the output
sub playsong($song, $quiet) {
  my $cmd = @play_commands.first({$song.match(.key)}).value;
  say "Playing via $cmd '$song'";
  shell("$cmd '$song' {$quiet ?? '>/dev/null 2>&1' !! ''}");
}

sub USAGE {
  say q:to[END];
    USAGE: polly6 [parameters] file1 dir1 ...
      -a: announce the current song (festival)
      -h: show this usage info
      -o: On Screen Display of song title (osdctl)
      -s: force sort (dirs are already sorted)
      -q: quiet the output of players
      -v: be verbose
      -z: shuffle the file list
    END
}

sub MAIN(
  *@songs,
  Bool :a(:$announce) = False,
  Bool :o(:$osd)      = False,
  Bool :s(:$sort)     = False,
  Bool :q(:$quiet)    = False,
  Bool :v(:$verbose)  = False,
  Bool :z(:$shuffle)  = False,
) {
  @songs ||= <.>;

  @songs = expand_songs($verbose, @songs);

  say "Playing {@songs.elems} songs";

  @songs .= pick(*) if $shuffle;
  @songs .= sort    if $sort;

  for @songs -> $song {
    osd_song($song)      if $osd;
    announce_song($song) if $announce;
    playsong($song, $quiet);
  }
}

